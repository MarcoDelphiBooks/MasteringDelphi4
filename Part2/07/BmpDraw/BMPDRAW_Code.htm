<HTML><HEAD>
<TITLE>Project: BMPDRAW.DPR</TITLE>
<META NAME="GENERATOR" CONTENT="PasToHTML[Marco Cantù]">
</HEAD>
<BODY BGCOLOR="#FFFFFF">
<CENTER><I>Marco Cant&ugrave; 1998, Mastering Delphi 4</I></CENTER>
<H1><CENTER>Project: BMPDRAW.DPR</CENTER></H1><HR>
<H3>Project Structure</H3><UL>
<LI> <A HREF=#BMPDRAW.DPR> BMPDRAW.DPR</A>
<LI> <A HREF=#BMPFORM.PAS> BMPFORM.PAS</A>
<LI> <A HREF=#BMPFORM.DFM> BMPFORM.DFM</A>
</UL>
<HR>
<A NAME=BMPDRAW.DPR><H3>BMPDRAW.DPR</H3></A>

<PRE>
<B>program</B> BmpDraw;

<B>uses</B>
  Forms,
  BmpForm <B>in</B> <I>'BmpForm.pas'</I> <FONT COLOR="#000080"><I>{BitmapForm}</I></FONT>;

<FONT COLOR="#000080"><I>{$R *.RES}</I></FONT>

<B>begin</B>
  Application.CreateForm(TBitmapForm, BitmapForm);
  Application.Run;
<B>end</B>.</PRE>
<HR>

<A NAME=BMPFORM.PAS><H3>BMPFORM.PAS</H3></A>

<PRE>
<B>unit</B> BmpForm;

<B>interface</B>

<B>uses</B>
  Windows, Classes, Graphics, Forms, Controls, Menus,
  Dialogs, StdCtrls, ExtCtrls, SysUtils, ComCtrls, Buttons;

<B>type</B>
  TBitmapForm = <B>class</B>(TForm)
    MainMenu1: TMainMenu;
    File1: TMenuItem;
    Open1: TMenuItem;
    N1: TMenuItem;
    Exit1: TMenuItem;
    Help1: TMenuItem;
    AboutImageViewer1: TMenuItem;
    OpenDialog1: TOpenDialog;
    Options1: TMenuItem;
    Image1: TImage;
    ColorDepth1: TMenuItem;
    GenerateSlow1: TMenuItem;
    GenerateFast1: TMenuItem;
    ScrollPanel1: TMenuItem;
    PanelScroll: TPanel;
    TrackBar1: TTrackBar;
    BtnCancel: TBitBtn;
    BtnGo: TBitBtn;
    Label1: TLabel;
    <B>procedure</B> Open1Click(Sender: TObject);
    <B>procedure</B> Exit1Click(Sender: TObject);
    <B>procedure</B> AboutImageViewer1Click(Sender: TObject);
    <B>procedure</B> ColorDepth1Click(Sender: TObject);
    <B>procedure</B> GenerateSlow1Click(Sender: TObject);
    <B>procedure</B> GenerateFast1Click(Sender: TObject);
    <B>procedure</B> ScrollPanel1Click(Sender: TObject);
    <B>procedure</B> TrackBar1Change(Sender: TObject);
    <B>procedure</B> BtnGoClick(Sender: TObject);
    <B>procedure</B> BtnCancelClick(Sender: TObject);
    <B>procedure</B> FormCreate(Sender: TObject);
  <B>private</B>
    nLines: Integer;
    fCancel: Boolean;
  <B>public</B>
    <FONT COLOR="#000080"><I>{ Public declarations }</I></FONT>
  <B>end</B>;

<B>var</B>
  BitmapForm: TBitmapForm;

<B>implementation</B>

              <FONT COLOR="#000080"><I>{$R *.DFM}</I></FONT>

<B>procedure</B> TBitmapForm.Open1Click(Sender: TObject);
<B>begin</B>
  <B>if</B> OpenDialog1.Execute <B>then</B>
  <B>begin</B>
    Image1.Picture.LoadFromFile (OpenDialog1.FileName);
    Caption := <I>'Image Viewer - '</I> + OpenDialog1.FileName;
  <B>end</B>;
<B>end</B>;

<B>procedure</B> TBitmapForm.Exit1Click(Sender: TObject);
<B>begin</B>
  Close;
<B>end</B>;

<B>procedure</B> TBitmapForm.AboutImageViewer1Click(Sender: TObject);
<B>begin</B>
  MessageDlg (<I>'Image Viewer is an example'</I>
    + <I>#13'of the book &quot;Mastering Delphi&quot;'</I>
    + <I>#13'written by Marco Cantù'</I>,
    mtInformation, [mbOk], 0);
<B>end</B>;

<B>procedure</B> TBitmapForm.ColorDepth1Click(Sender: TObject);
<B>var</B>
  strDepth: <B>String</B>;
<B>begin</B>
  <B>case</B> Image1.Picture.Bitmap.PixelFormat <B>of</B>
    pfDevice: strDepth := <I>'Device'</I>;
    pf1bit: strDepth := <I>'1-bit'</I>;
    pf4bit: strDepth := <I>'4-bit'</I>;
    pf8bit: strDepth := <I>'8-bit'</I>;
    pf15bit: strDepth := <I>'15-bit'</I>;
    pf16bit: strDepth := <I>'16-bit'</I>;
    pf24bit: strDepth := <I>'24-bit'</I>;
    pf32bit: strDepth := <I>'32-bit'</I>;
    pfCustom: strDepth := <I>'Custom'</I>;
  <B>end</B>;
  MessageDlg (<I>'Bitmap color depth: '</I> + strDepth,
    mtInformation, [mbOK], 0);
<B>end</B>;

<B>procedure</B> TBitmapForm.GenerateSlow1Click(Sender: TObject);
<B>var</B>
  Bmp: TBitmap;
  I, J: Integer;
  T: Cardinal;
<B>begin</B>
  <FONT COLOR="#000080"><I>// get the image and modify it</I></FONT>
  Bmp := Image1.Picture.Bitmap;
  Bmp.PixelFormat := pf24bit;
  Bmp.Width := 256;
  Bmp.Height := 256;

  T := GetTickCount;
  <FONT COLOR="#000080"><I>// change every pixel</I></FONT>
  <B>for</B> I := 0 <B>to</B> Bmp.Height - 1 <B>do</B>
    <B>for</B> J := 0 <B>to</B> Bmp.Width - 1 <B>do</B>
      Bmp.Canvas.Pixels [I, J] := RGB (I*J <B>mod</B> 255, I, J);
  Caption := <I>'Image Viewer - Memory Image (MSecs: '</I> +
    IntToStr (GetTickCount - T) + <I>')'</I>;
<B>end</B>;

<B>procedure</B> TBitmapForm.GenerateFast1Click(Sender: TObject);
<B>var</B>
  Bmp: TBitmap;
  I, J: Integer;
  T: Cardinal;
  Line: PByteArray;
<B>begin</B>
  <FONT COLOR="#000080"><I>// get the image and modify it</I></FONT>
  Bmp := Image1.Picture.Bitmap;
  Bmp.PixelFormat := pf24bit;
  Bmp.Width := 256;
  Bmp.Height := 256;

  T := GetTickCount;
  <FONT COLOR="#000080"><I>// change every pixel, line by line</I></FONT>
  <B>for</B> I := 0 <B>to</B> Bmp.Height - 1 <B>do</B>
  <B>begin</B>
    Line := PByteArray (Bmp.ScanLine [I]);
    <B>for</B> J := 0 <B>to</B> Bmp.Width - 1 <B>do</B>
    <B>begin</B>
      Line [J*3] := J;
      Line [J*3+1] := I*J <B>mod</B> 255;
      Line [J*3+2] := I;
    <B>end</B>;
  <B>end</B>;
  <FONT COLOR="#000080"><I>// refresh the video</I></FONT>
  Image1.Invalidate;
  Caption := <I>'Image Viewer - Memory Image (MSecs: '</I> +
    IntToStr (GetTickCount - T) + <I>')'</I>;
<B>end</B>;

<B>procedure</B> TBitmapForm.ScrollPanel1Click(Sender: TObject);
<B>begin</B>
  PanelScroll.Visible := <B>not</B> PanelScroll.Visible;
  ScrollPanel1.Checked := PanelScroll.Visible;
<B>end</B>;

<B>procedure</B> TBitmapForm.TrackBar1Change(Sender: TObject);
<B>begin</B>
  nLines := TrackBar1.Position;
  TrackBar1.Hint := IntToStr (TrackBar1.Position);
<B>end</B>;

<B>procedure</B> TBitmapForm.BtnGoClick(Sender: TObject);
<B>var</B>
  W, H, I, J, LineBytes: Integer;
  Line: PByteArray;
  Bmp: TBitmap;
  R: TRect;
<B>begin</B>
  <FONT COLOR="#000080"><I>// set the user interface</I></FONT>
  fCancel := False;
  BtnGo.Enabled := False;
  BtnCancel.Enabled := True;

  <FONT COLOR="#000080"><I>// get the bitmap of the image and resize it</I></FONT>
  Bmp := Image1.Picture.Bitmap;
  W := Bmp.Width;
  H := Bmp.Height;

  <FONT COLOR="#000080"><I>// allocate enough memory for one line</I></FONT>
  LineBytes := Abs (Integer (Bmp.ScanLine [1]) -
    Integer (Bmp.ScanLine [0]));
  Line := AllocMem (LineBytes);

  <FONT COLOR="#000080"><I>// scroll how many itmes as there are lines</I></FONT>
  <B>for</B> I := 0 <B>to</B> H - 1 <B>do</B>
  <B>begin</B>
    <FONT COLOR="#000080"><I>// exit the for loop if Cancel was pressed</I></FONT>
    <B>if</B> fCancel <B>then</B>
      Break;
    <FONT COLOR="#000080"><I>// copy the first line</I></FONT>
    Move ((Bmp.ScanLine [0])^, Line^, LineBytes);
    <FONT COLOR="#000080"><I>// for every line</I></FONT>
    <B>for</B> J := 1 <B>to</B> H - 1 <B>do</B>
    <B>begin</B>
      <FONT COLOR="#000080"><I>// move line to the previous one</I></FONT>
      Move ((Bmp.ScanLine [J])^, (Bmp.ScanLine [J-1])^, LineBytes);
      <FONT COLOR="#000080"><I>// every nLines update the output</I></FONT>
      <B>if</B> (J <B>mod</B> nLines = 0) <B>then</B>
      <B>begin</B>
        R := Rect (0, PanelScroll.Height + J-nLines,
          W, PanelScroll.Height + J);
        InvalidateRect (Handle, @R, False);
        UpdateWindow (Handle);
      <B>end</B>;
    <B>end</B>;
    <FONT COLOR="#000080"><I>// move back the first line to the end</I></FONT>
    Move (Line^, (Bmp.ScanLine [Bmp.Height - 1])^, LineBytes);
    <FONT COLOR="#000080"><I>// update the final portion of the bitmap</I></FONT>
    R := Rect (0, PanelScroll.Height + H - nLines,
      W, PanelScroll.Height + H);
    InvalidateRect (Handle, @R, False);
    UpdateWindow (Handle);
    <FONT COLOR="#000080"><I>// let the program handle other messages</I></FONT>
    Application.ProcessMessages;
  <B>end</B>;
  <FONT COLOR="#000080"><I>// reset the UI</I></FONT>
  BtnGo.Enabled := True;
  BtnCancel.Enabled := False;
<B>end</B>;

<B>procedure</B> TBitmapForm.BtnCancelClick(Sender: TObject);
<B>begin</B>
  fCancel := True;
<B>end</B>;

<B>procedure</B> TBitmapForm.FormCreate(Sender: TObject);
<B>begin</B>
  TrackBar1Change (self);
 <B>end</B>;

<B>end</B>.</PRE>
<HR>

<A NAME=BMPFORM.DFM><H3>BMPFORM.DFM</H3></A>

<PRE>
<B>object</B> BitmapForm: TBitmapForm
  Left = 245
  Top = 131
  Width = 475
  Height = 385
  Caption = <I>'Bitmap Draw'</I>
  Color = clBtnFace
  Font.Charset = ANSI_CHARSET
  Font.Color = clWindowText
  Font.Height = -13
  Font.Name = <I>'Arial'</I>
  Font.Style = []
  Menu = MainMenu1
  OldCreateOrder = True
  OnCreate = FormCreate
  PixelsPerInch = 96
  TextHeight = 16
  <B>object</B> Image1: TImage
    Left = 0
    Top = 49
    Width = 467
    Height = 290
    Align = alClient
  <B>end</B>
  <B>object</B> PanelScroll: TPanel
    Left = 0
    Top = 0
    Width = 467
    Height = 49
    Align = alTop
    ParentShowHint = False
    ShowHint = True
    TabOrder = 0
    Visible = False
    <B>object</B> Label1: TLabel
      Left = 216
      Top = 16
      Width = 41
      Height = 16
      Caption = <I>'Speed:'</I>
    <B>end</B>
    <B>object</B> TrackBar1: TTrackBar
      Left = 264
      Top = 9
      Width = 150
      Height = 31
      Max = 210
      Min = 10
      Orientation = trHorizontal
      PageSize = 40
      Frequency = 20
      Position = 110
      SelEnd = 0
      SelStart = 0
      TabOrder = 0
      ThumbLength = 20
      TickMarks = tmBottomRight
      TickStyle = tsAuto
      OnChange = TrackBar1Change
    <B>end</B>
    <B>object</B> BtnCancel: TBitBtn
      Left = 104
      Top = 11
      Width = 75
      Height = 25
      Caption = <I>'&amp;Cancel'</I>
      Enabled = False
      TabOrder = 1
      OnClick = BtnCancelClick
      Kind = bkCancel
    <B>end</B>
    <B>object</B> BtnGo: TBitBtn
      Left = 16
      Top = 11
      Width = 75
      Height = 25
      Caption = <I>'&amp;Go'</I>
      TabOrder = 2
      OnClick = BtnGoClick
      Kind = bkIgnore
    <B>end</B>
  <B>end</B>
  <B>object</B> MainMenu1: TMainMenu
    Left = 24
    Top = 56
    <B>object</B> File1: TMenuItem
      Caption = <I>'&amp;File'</I>
      <B>object</B> Open1: TMenuItem
        Caption = <I>'&amp;Open...'</I>
        OnClick = Open1Click
      <B>end</B>
      <B>object</B> N1: TMenuItem
        Caption = <I>'-'</I>
      <B>end</B>
      <B>object</B> Exit1: TMenuItem
        Caption = <I>'&amp;Exit'</I>
        OnClick = Exit1Click
      <B>end</B>
    <B>end</B>
    <B>object</B> Options1: TMenuItem
      Caption = <I>'&amp;Options'</I>
      <B>object</B> ColorDepth1: TMenuItem
        Caption = <I>'Color &amp;Depth'</I>
        OnClick = ColorDepth1Click
      <B>end</B>
      <B>object</B> GenerateSlow1: TMenuItem
        Caption = <I>'&amp;Generate (Slow)'</I>
        OnClick = GenerateSlow1Click
      <B>end</B>
      <B>object</B> GenerateFast1: TMenuItem
        Caption = <I>'Generate (&amp;Fast)'</I>
        OnClick = GenerateFast1Click
      <B>end</B>
      <B>object</B> ScrollPanel1: TMenuItem
        Caption = <I>'Scroll Panel'</I>
        OnClick = ScrollPanel1Click
      <B>end</B>
    <B>end</B>
    <B>object</B> Help1: TMenuItem
      Caption = <I>'&amp;Help'</I>
      <B>object</B> AboutImageViewer1: TMenuItem
        Caption = <I>'&amp;About Bitmap Draw...'</I>
        OnClick = AboutImageViewer1Click
      <B>end</B>
    <B>end</B>
  <B>end</B>
  <B>object</B> OpenDialog1: TOpenDialog
    Filter = <I>'Bitmap (*.bmp)|*.bmp'</I>
    Options = [ofHideReadOnly, ofPathMustExist, ofFileMustExist]
    Left = 24
    Top = 104
  <B>end</B>
<B>end</B></PRE>
<HR>

<BR><I><CENTER>Copyright Marco Cant&ugrave; 1998</CENTER></I>
</BODY> </HTML>